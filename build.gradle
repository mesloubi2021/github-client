buildscript {
  ext.kotlin_version = '1.4.21'
  repositories {
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.2'
    classpath('com.google.firebase:firebase-plugins:2.0.0') {
      exclude group: 'com.google.api-client', module: 'google-api-client' // conflicts with com.github.triplet.play
    }
    classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
    classpath 'com.google.gms:google-services:4.3.4'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects {
  project.plugins.whenPluginAdded { plugin ->
    if ('com.android.build.gradle.AppPlugin' == plugin.class.name
      || 'com.android.build.gradle.LibraryPlugin' == plugin.class.name) {

      project.android.testOptions.unitTests.all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          exceptionFormat 'full'
        }
      }
    }
  }
}

subprojects {
  if (!gradle.startParameter.taskNames.any { it.contains("lint") }) {
    tasks.configureEach {
      if (name.startsWith("lint")) {
        enabled = false
      }
    }
  }
}

ext {
  daggerVersion = '2.31.2'
  daggerAnnotationProcessor = "com.google.dagger:dagger-compiler:$daggerVersion"
  dagger = "com.google.dagger:dagger:$daggerVersion"

  okHttpVersion = '4.9.0'
  okHttp = "com.squareup.okhttp3:okhttp:$okHttpVersion"
  okHttpLoggingInterceptor = "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

  frescoVesion = '2.3.0'
  fresco = "com.facebook.fresco:fresco:$frescoVesion"
  frescoDrawee = "com.facebook.fresco:drawee:$frescoVesion"
}

apply from: 'dependencies.gradle'
